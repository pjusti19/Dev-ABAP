************************************************************
*
*  Programa Z14_EXERC7_1_v2
*
*  Data: 05.02.2025
*
*  Programa: Exemplo de batch-input – alterar valores de materiais – Parte 1
*
*  Autor: Pedro Justi
*
************************************************************
REPORT z14_exerc7_1_v2.

CONSTANTS modo_transacao TYPE c VALUE `N`.

CLASS lcl_material DEFINITION.
  PUBLIC SECTION.
    TYPES: BEGIN OF y_dados_material,
             codigo_material(18) TYPE c,
             descricao(40)       TYPE c,
             nmr_antigo(18)      TYPE c,
             nmr_desenho(22)     TYPE c,
           END OF y_dados_material,
           yt_dados_material TYPE TABLE OF y_dados_material.

    METHODS:
      carregar_dados_arquivo
        IMPORTING iv_caminho_arquivo TYPE string
        EXPORTING et_dados_materiais TYPE yt_dados_material,

      monta_batch_input
        IMPORTING codigo_material TYPE c
                  nmr_antigo      TYPE c
                  nmr_desenho     TYPE c,

      atualizar_materiais
        IMPORTING it_dados_materiais TYPE yt_dados_material,

      monta_linha_batch_input
        IMPORTING program  TYPE any OPTIONAL
                  dynpro   TYPE any OPTIONAL
                  dynbegin TYPE any OPTIONAL
                  fnam     TYPE any OPTIONAL
                  fval     TYPE any OPTIONAL.


  PRIVATE SECTION.
    DATA: t_batch_input TYPE TABLE OF bdcdata,
          t_mensagens   TYPE TABLE OF bdcmsgcoll.

ENDCLASS.

CLASS lcl_material IMPLEMENTATION.

  METHOD carregar_dados_arquivo.

    CALL FUNCTION 'GUI_UPLOAD'
      EXPORTING
        filename                = iv_caminho_arquivo
        has_field_separator     = 'X'
      TABLES
        data_tab                = et_dados_materiais
      EXCEPTIONS
        file_open_error         = 1
        file_read_error         = 2
        no_batch                = 3
        gui_refuse_filetransfer = 4
        invalid_type            = 5
        no_authority            = 6
        unknown_error           = 7
        bad_data_format         = 8
        header_not_allowed      = 9
        separator_not_allowed   = 10
        header_too_long         = 11
        unknown_dp_error        = 12
        access_denied           = 13
        dp_out_of_memory        = 14
        disk_full               = 15
        dp_timeout              = 16
        OTHERS                  = 17.
    IF sy-subrc <> 0.
      MESSAGE i897(sd) WITH 'Erro na leitura do arquivo'.
    ENDIF.
    DELETE et_dados_materiais INDEX 1. "Removendo o cabecalho

  ENDMETHOD.

  METHOD atualizar_materiais.

    Data: v_modo_transacao(1) type c.

    LOOP AT it_dados_materiais ASSIGNING FIELD-SYMBOL(<material>).

      monta_batch_input( codigo_material = <material>-codigo_material
                         nmr_antigo = <material>-nmr_antigo
                         nmr_desenho = <material>-nmr_desenho ).

      CALL TRANSACTION 'MM02' USING t_batch_input
                              MODE modo_transacao
                              MESSAGES INTO t_mensagens.

    ENDLOOP.

  ENDMETHOD.


  METHOD monta_batch_input.

    REFRESH t_batch_input.

*   Tela de leitura do codigo do material
    monta_linha_batch_input( program = `SAPLMGMM` dynpro = 0060 dynbegin = `X`).
    monta_linha_batch_input( fnam = `BDC_OKCODE` fval = `=ENTR`).
    monta_linha_batch_input( fnam = `RMMG1-MATNR` fval = codigo_material ).

*   Tela de selecao de exibicao
    monta_linha_batch_input( program = `SAPLMGMM` dynpro = 0070 dynbegin = `X`).
    monta_linha_batch_input( fnam = `BDC_OKCODE` fval = `=ENTR`).
    monta_linha_batch_input( fnam = `MSICHTAUSW-KZSEL(01)` fval = `X`).

*   Tela dos dados basicos 1
    monta_linha_batch_input( program = `SAPLMGMM` dynpro = 4004 dynbegin = `X`).
    monta_linha_batch_input(  fnam = `BDC_OKCODE` fval = `=SP02`).
    monta_linha_batch_input(  fnam = `MARA-BISMT` fval = nmr_antigo ).

*   Tela dos dados basicos 1
    monta_linha_batch_input( program = `SAPLMGMM` dynpro = 4004 dynbegin = `X`).
    monta_linha_batch_input(  fnam = `BDC_OKCODE` fval = `=BU`).
    monta_linha_batch_input(  fnam = `MARA-ZEINR` fval = nmr_desenho ).

  ENDMETHOD.

  METHOD monta_linha_batch_input.

    APPEND INITIAL LINE TO t_batch_input ASSIGNING FIELD-SYMBOL(<batch>).
    IF program IS SUPPLIED.
      <batch>-program = program.
      <batch>-dynpro  = dynpro.
      <batch>-dynbegin = dynbegin.
    ELSE.
      <batch>-fnam = fnam.
      <batch>-fval = fval.
    ENDIF.

  ENDMETHOD.


ENDCLASS.

START-OF-SELECTION.

  PARAMETERS: nm_arq TYPE string.

  DATA: lo_material        TYPE REF TO lcl_material,
        lt_dados_materiais TYPE lcl_material=>yt_dados_material.

  lo_material = NEW lcl_material( ).

  lo_material->carregar_dados_arquivo( EXPORTING iv_caminho_arquivo = nm_arq
                                       IMPORTING et_dados_materiais = lt_dados_materiais ).

  lo_material->atualizar_materiais( it_dados_materiais = lt_dados_materiais ).
